plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.yachaerang'
version = '0.0.1-SNAPSHOT'
description = 'batch'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

jacoco {
    toolVersion = '0.8.12'
}

repositories {
    mavenCentral()
}

dependencies {
    // Batch
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    testImplementation 'org.springframework.batch:spring-batch-test'

    // JDBC
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.5'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Flyway
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.flywaydb:flyway-core:9.8.1'
    implementation 'org.flywaydb:flyway-mysql:9.8.1'
}

ext.jacocoExclusions = [
        '**/*Application*',
        '**/config/**',
        '**/dto/**',
        '**/entity/**',
        '**/common/**',
        '**/test/**',
        '**/*Test*',
        '**/*Filter*',
        '**/util/**',
        '**/exception/**',
        '**/response/**'
]

tasks.named('jacocoTestReport') {
    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExclusions)
            })
    )
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn(tasks.named('jacocoTestReport'))

    classDirectories.setFrom(
            files(tasks.named('jacocoTestReport').get().classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExclusions)
            })
    )

    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/test.exec").get().asFile
    }
}
