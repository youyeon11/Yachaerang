plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'jacoco'
}

group = 'com.yachaerang'
version = '0.0.1-SNAPSHOT'
description = 'backend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

jacoco {
    toolVersion = '0.8.12'
}

repositories {
    mavenCentral()
    maven {url 'https://repo.spring.io/milestone'}
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:1.1.1"
    }
}

dependencies {
    // Spring Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // REST Docs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.1'

    // Asciidoctor
    implementation 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.1'

    // MySQL
    implementation 'mysql:mysql-connector-java:8.0.33'

    // Flyway
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.flywaydb:flyway-core:9.8.1'
    implementation 'org.flywaydb:flyway-mysql:9.8.1'

    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.3'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // H2
    testImplementation 'com.h2database:h2'

    // logging
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    // Spring AI
    implementation 'org.springframework.ai:spring-ai-starter-model-google-genai'
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'

    // WebFlux
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // S3
    implementation platform("software.amazon.awssdk:bom:2.25.65")
    implementation 'software.amazon.awssdk:s3'

    // SMTP
    implementation 'org.springframework.boot:spring-boot-starter-mail'
}

def snippetsDir = layout.buildDirectory.dir("generated-snippets")
def jacocoReportDir = layout.buildDirectory.dir("reports/jacoco/html")

ext.jacocoExclusions = [
        '**/*Application*',
        '**/config/**',
        '**/dto/**',
        '**/entity/**',
        '**/common/**',
        '**/test/**',
        '**/*Test*',
        '**/*Filter*',
        '**/util/**',
        '**/exception/**',
        '**/response/**'
]

tasks.register('copySnippets', Copy) {
    dependsOn 'asciidoctor'
    from(snippetsDir)
    into("src/docs")
}

tasks.named('jacocoTestReport') {
    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExclusions)
            })
    )
    dependsOn test
    reports {
        html.required = true
        html.outputLocation.set(jacocoReportDir)
        xml.required = false
        csv.required = false
    }
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn(tasks.named('jacocoTestReport'))

    classDirectories.setFrom(
            files(tasks.named('jacocoTestReport').get().classDirectories.files.collect {dir ->
                fileTree(dir: dir, exclude: jacocoExclusions)
            })
    )

    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }
        }
    }
}

tasks.named('asciidoctor') {
    dependsOn test
    inputs.dir snippetsDir

    sourceDir = file('src/docs')
    outputDir = file("${buildDir}/docs/asciidoc")

    attributes 'snippets': snippetsDir

    configurations 'asciidoctorExt'

    sources {
        include 'index.adoc'
    }
}

tasks.named('bootJar') {
    dependsOn tasks.named('asciidoctor')

    from(layout.buildDirectory.dir('docs/asciidoc')) {
        into 'BOOT-INF/classes/static/docs'
    }
}

tasks.named('test') {
    useJUnitPlatform()
    outputs.dir snippetsDir
    finalizedBy jacocoTestReport
    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/test.exec").get().asFile
    }
}